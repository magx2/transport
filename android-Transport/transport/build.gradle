buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // replace with the current version of the Android plugin
        classpath 'com.android.tools.build:gradle:0.9.2'
        // the latest version of the android-apt plugin
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.1'
    }
}
apply plugin: 'android'
apply plugin: 'android-apt'

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    apt
}

def AAVersion = '3.0.1'
def ORMLiteVersion = '4.46'
def mainPackage = 'pl.grzeslowski.transport'
def paidPackage = mainPackage + '.paid'
def freePackage = mainPackage + '.free'

dependencies {
    compile 'com.android.support:appcompat-v7:19.1.0'
    compile 'com.android.support:support-v4:19.1.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android annotations
    apt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"

    // ORMLite
    compile "com.j256.ormlite:ormlite-android:$ORMLiteVersion"
    compile "com.j256.ormlite:ormlite-core:$ORMLiteVersion"

    // Google Guava
    compile 'com.google.guava:guava:16.0.1'

    // Joda Time
    compile 'joda-time:joda-time:2.3'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            buildConfigField "java.lang.String", "DATABASE_NAME", "\"transporter\""
            buildConfigField "int", "DATABASE_VERSION", "1"

            buildConfigField "java.lang.String", "PAID_PACKAGE_NAME", "\"$paidPackage\""
            buildConfigField "java.lang.String", "FREE_PACKAGE_NAME", "\"$freePackage\""
        }

        debug {
            debuggable true
            runProguard false

            buildConfigField "java.lang.String", "DATABASE_NAME", "\"transporter\""
            buildConfigField "int", "DATABASE_VERSION", "1"

            buildConfigField "java.lang.String", "PAID_PACKAGE_NAME", "\"$paidPackage\""
            buildConfigField "java.lang.String", "FREE_PACKAGE_NAME", "\"$freePackage\""
        }
    }

    productFlavors {
        free {
            packageName freePackage
            buildConfigField "pl.grzeslowski.transport.product_flavors.MonetizationType", "MONETIAZATION_TYPE", "pl.grzeslowski.transport.product_flavors.MonetizationType.FREE"
        }

        paid {
            packageName paidPackage
            buildConfigField "pl.grzeslowski.transport.product_flavors.MonetizationType", "MONETIAZATION_TYPE", "pl.grzeslowski.transport.product_flavors.MonetizationType.PAID"
        }
    }

    // Database Configs
}

def getSourceSetName(variant) {
    return new File(variant.dirName).getName();
}

android.applicationVariants.all { variant ->
    def aptOutputDir = project.file("build/source/apt")
    def aptOutput = new File(aptOutputDir, variant.dirName)
    println "****************************"
    println "variant: ${variant.name}"
    println "manifest: ${variant.processResources.manifestFile}"
    println "aptOutput: ${aptOutput}"
    println "****************************"

    android.sourceSets[getSourceSetName(variant)].java.srcDirs += aptOutput.getPath()

//    variant.javaCompile.options.compilerArgs += [
//            '-processorpath', configurations.apt.getAsPath(),
//            '-AandroidManifestFile=' + variant.processResources.manifestFile,
//            '-s', aptOutput
//    ]

    variant.javaCompile.options.compilerArgs += [
            '-processorpath', configurations.apt.getAsPath(),
            '-AandroidManifestFile=' + variant.processResources.manifestFile,
            '-AresourcePackageName=' + mainPackage,
            '-s', aptOutput
    ]

    variant.javaCompile.source = variant.javaCompile.source.filter { p ->
        return !p.getPath().startsWith(aptOutputDir.getPath())
    }

    variant.javaCompile.doFirst {
        aptOutput.mkdirs()
    }
}